# Maintainer: Luis David Licea Torres <luisliceatorres@gmail.com>
# shellcheck disable=2034,2154,2148
# vim: filetype=sh

_pkgname=zict
pkgname=zict-git
pkgver=r31.c9d8b70
pkgrel=1
pkgdesc='A command-line dictionary based on zim and w3m.'
arch=(x86_64)
url=https://github.com/luis-licea/zict
license=(GPL3)
depends=(zim-tools w3m xorg-setxkbmap)
makedepends=(git)
provides=("$_pkgname")
conflicts=("$_pkgname")
replaces=("$_pkgname")
optdepends=(
    'bash: An interactive shell that offers word completion'
    'bash-completion: Auto-complete and suggest words using tab'
    'zsh: An interactive shell that offers word completion'
)
source=("$_pkgname::git+$url#branch=master")
md5sums=(SKIP)

pkgver() {
    cd "$_pkgname" || exit
    # Use for Git when there are no tags available.
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
    cd "$_pkgname" || exit

    # Install license.
    find . -name LICENSE-GPL3.txt -type f -exec install -Dm644 {} \
        "$pkgdir/usr/share/licenses/$_pkgname/LICENSE-GPL3.txt" \;

    # Install shell completion files.
    find . -name complete.zsh -type f -exec install -Dm644 {} \
        "$pkgdir/usr/share/zsh/site-functions/_$_pkgname" \;
    find . -name complete.bash -type f -exec install -Dm644 {} \
        "$pkgdir/usr/share/bash-completion/completions/$_pkgname" \;

    # Install executable.
    install -Dm 755 "$_pkgname" "$pkgdir/usr/bin/$_pkgname"

    # Install config file.
    find . -name zict.bash -type f -exec install -Dm644 {} \
        "$pkgdir/etc/$_pkgname/zict.bash" \;

}
